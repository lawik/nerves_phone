defmodule NervesPhone.Audio.Mic do
  @moduledoc """
  The microphones.

  The phone has two digital mics on its WCD9335 codec: `DMIC1` at the
  bottom, the one you speak into, and `DMIC2` at the top, a reference for
  the surrounding noise. Nothing in the sound stack cancels noise or echo
  for a recording (the ADSP only does that inside cellular calls), so the
  top mic is only of use to code that processes the pair itself, and a
  hindrance to anything that just wants a voice. It is therefore off by
  default: `ex_audio` routes only the bottom mic into the capture device at
  boot (`config :ex_audio` in `config/fp3.exs`), and recording from ALSA
  device `mic` (or `hw:0,1`) is mono.

  `set_secondary(true)` adds the top mic as a second channel, on the
  codec's SLIM TX8 port next to the bottom mic's TX7. The capture device
  then has to be opened with two channels: the codec sends one channel per
  mic port that is switched on, and a recording that asks for a different
  count gets silence. Channel 0 is the bottom mic, channel 1 the top one.
  `channels/0` tells the count to open; the top mic is off again after a
  reboot.

  So that a mono recording always works, ALSA device `mic` is a plug over
  `hw:0,1` pinned to the current channel count: the bottom mic alone by
  default, both mics mixed once the top one is on. Its definition lives in
  `state_dir/asound.mic.conf`, which `rootfs_overlay/etc/asound.conf` loads
  and this process writes.

  **The two-channel path does not work yet.** On nerves_system_fp3 v0.2.4
  (kernel 6.19.5-msm8953) a two-channel capture with both ports switched
  on records only zeros, and opening the capture device with one channel
  while two ports are on crashes the kernel (the phone reboots; the trace
  is in `/sys/fs/pstore`). Until the kernel's SLIMbus capture carries two
  ports, `set_secondary(true)` returns `{:error, :unsupported}` unless
  `config :nerves_phone, mic_secondary: :experimental` is set, so that
  nothing can stumble into the crash by accident.

  On the host nothing is routed (no `mic_control` set); only the file is
  kept, so the rest of the app behaves the same.
  """

  use GenServer
  require Logger

  @device "mic"

  # Mixer settings that put the top mic on SLIM TX8 (decimator 8), and that
  # take it off again. The bottom mic on TX7 is routed by ex_audio at boot.
  @secondary_on [
    {"SLIM TX8 MUX", "DEC8"},
    {"ADC MUX8", "DMIC"},
    {"DMIC MUX8", "DMIC2"},
    {"AIF1_CAP Mixer SLIM TX8", "on"}
  ]
  @secondary_off [
    {"AIF1_CAP Mixer SLIM TX8", "off"},
    {"DMIC MUX8", "ZERO"},
    {"SLIM TX8 MUX", "ZERO"}
  ]

  def start_link(opts \\ []), do: GenServer.start_link(__MODULE__, opts, name: __MODULE__)

  @doc "The ALSA device to record from."
  def device, do: @device

  @doc "Whether the top mic is on."
  @spec secondary?() :: boolean()
  def secondary?, do: GenServer.call(__MODULE__, :secondary?)

  @doc "The number of channels to open the capture device with: 1, or 2 with the top mic on."
  @spec channels() :: 1 | 2
  def channels, do: if(secondary?(), do: 2, else: 1)

  @doc """
  Turns the top mic on or off as the second capture channel.

  Returns `{:error, :unsupported}` unless `mic_secondary: :experimental`
  is configured (see the module doc), and `{:error, reason}` without
  changing anything if the mixer refuses, for instance before the sound
  card is up.
  """
  @spec set_secondary(boolean()) :: :ok | {:error, term()}
  def set_secondary(on?) when is_boolean(on?),
    do: GenServer.call(__MODULE__, {:set_secondary, on?})

  @doc "The file that defines ALSA device `mic`."
  def conf_path, do: Path.join(NervesPhone.state_dir(), "asound.mic.conf")

  @impl GenServer
  def init(_opts) do
    # Off by default, also after a restart of this process alone. The card
    # may not be up yet; then the switches are at their power-on default
    # (off) anyway.
    _ = apply_mixer(@secondary_off)
    write_conf(1)
    {:ok, %{secondary: false}}
  end

  @impl GenServer
  def handle_call(:secondary?, _from, state), do: {:reply, state.secondary, state}

  def handle_call({:set_secondary, on?}, _from, %{secondary: on?} = state),
    do: {:reply, :ok, state}

  def handle_call({:set_secondary, true}, _from, state) do
    if Application.get_env(:nerves_phone, :mic_secondary) == :experimental do
      switch(true, state)
    else
      {:reply, {:error, :unsupported}, state}
    end
  end

  def handle_call({:set_secondary, false}, _from, state), do: switch(false, state)

  defp switch(on?, state) do
    settings = if on?, do: @secondary_on, else: @secondary_off

    case apply_mixer(settings) do
      :ok ->
        write_conf(if on?, do: 2, else: 1)
        Logger.info("mic: top mic #{if on?, do: "on", else: "off"}")
        {:reply, :ok, %{state | secondary: on?}}

      {:error, reason} = error ->
        Logger.warning("mic: could not switch the top mic: #{inspect(reason)}")
        {:reply, error, state}
    end
  end

  # `config :nerves_phone, mic_control: :alsa` on the phone.
  defp apply_mixer(settings) do
    case Application.get_env(:nerves_phone, :mic_control) do
      :alsa -> Enum.reduce_while(settings, :ok, fn s, :ok -> cset(s) end)
      _ -> :ok
    end
  end

  defp cset({name, value}) do
    args = ["-q", "-c", "0", "cset", "name=#{name}", value]

    case System.cmd("amixer", args, stderr_to_stdout: true) do
      {_, 0} -> {:cont, :ok}
      {output, status} -> {:halt, {:error, {name, status, String.trim(output)}}}
    end
  rescue
    e -> {:halt, {:error, {name, e}}}
  end

  defp write_conf(channels) do
    File.mkdir_p(NervesPhone.state_dir())

    File.write(conf_path(), """
    # Written by NervesPhone.Audio.Mic; see rootfs_overlay/etc/asound.conf.
    # #{channels} channel(s): the sound card's capture device must be opened
    # with as many channels as mic ports are routed into it.
    pcm.#{@device} {
      type plug
      slave {
        pcm "hw:0,1"
        channels #{channels}
      }
    }
    """)
  end
end
